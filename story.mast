
#
# Show the proper GUI for server vs Client Consoles
#
reroute server server_main
reroute clients client_main
route change console select_console

start_text = "A starter project written in mast"

======= server_main =====

#
# Create the player ship so the clients see it
# This is a simple script that has one playable ship
#
simulation create
delay gui 1s
shared artemis =  to_id(player_spawn(0,0,0, "Artemis", "tsn", "tsn_battle_cruiser"))
do assign_client_to_ship(0,artemis)

section style="area:2,20,80,35;"

""" {start_text}"""

await choice:
    + "Start":
        ->> start
end_await
### This continues on to the next label

===== end_game =======
# 
# Check for end game
#
# no more players
players = role("__PLAYER__")
if len(players) == 0:
    start_text = "You lost"
    simulation pause
    -> server_main
end_if
#
# No more enemies
#
raiders = role("raider")
if len(raiders) == 0:
    start_text = "You won"
    simulation pause
    -> server_main
end_if
delay sim 5s
-> end_game

======== start ======
simulation resume
# Create the world here

# Create a space station
ds1 = npc_spawn(1000,0,1000, "DS1", "tsn, Station", "starbase_command", "behav_station")

# Create an enemy
k001 = npc_spawn(-1000,0,1000, "K001", "raider", "kralien_dreadnaught", "behav_npcship")

<<-


====== client_main =======


#
# Default the console to helm
#
console_select = "helm"

====== select_console ======

section style="area:2,20,80,35;"

"""Select your console"""

section style="area: 85,50, 99,90;"
vradio console_select "helm,weapons, comms,science,engineering"
blank
row

button "accept":
    ->console_selected
end_button

await gui
->END

====== console_selected ====
do assign_client_to_ship(client_id,artemis)
console console_select
await gui
