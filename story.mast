
#
# Show the proper GUI for server vs Client Consoles
#
gui_reroute_server( server_main)
gui_reroute_clients(client_main)
route_change_console("select_console")

start_text = "A starter project written in mast"

======= server_main =====

#
# Create the player ship so the clients see it
# This is a simple script that has one playable ship
#
sim_create()
await delay_app(1)
shared artemis =  to_id(player_spawn(0,0,0, "Artemis", "tsn", "tsn_battle_cruiser"))
assign_client_to_ship(0,artemis)

gui_section(style="area:2,20,80,35;")

""" {start_text}"""

await gui():
    + "Start":
        ->> start

### This continues on to the next label

===== end_game =======
# 
# Check for end game
#
# no more players
players = role("__PLAYER__")
if len(players) == 0:
    start_text = "You lost"
    sim_pause()
    -> server_main

#
# No more enemies
#
raiders = role("raider")
if len(raiders) == 0:
    start_text = "You won"
    sim_pause()
    -> server_main

await delay_sim(5)
-> end_game

======== start ======
sim_resume()
# Create the world here

# Create a space station
ds1 = npc_spawn(1000,0,1000, "DS1", "tsn, Station", "starbase_command", "behav_station")

# Create an enemy
k001 = npc_spawn(-1000,0,1000, "K001", "raider", "kralien_cruiser", "behav_npcship")

<<-


====== client_main =======


#
# Default the console to helm
#
console_select = "helm"

====== select_console ======

gui_section(style="area:2,20,80,35;")

"""Select your console"""

gui_section(style="area: 85,50, 99,90;")
gui_vradio("helm,weapons, comms,science,engineering", var="console_select")
gui_blank()
gui_row()

on gui_message(gui_button("accept")):
    ->console_selected

await gui()
->END

====== console_selected ====
assign_client_to_ship(client_id,artemis)
gui_console(console_select)
await gui()
